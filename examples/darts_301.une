# Darts 301

# Helpers.

pop = (list, index) ->
{
	new = []
	for i from 0 till len(list)
		if i != index
			new += [list[i]]
	return new
}

find = (list, value) ->
{
	for i from 0 till len(list)
		if list[i] == value
			return i
	return Void
}

widest = (list) ->
{
	max = 0
	for element in list {
		length = len(str(element))
		if length > max
			max = length
	}
	return max
}

sum = (expression) ->
{
	replacements = [[" ", "+"], [",", "+"], [";", "+"], ["x", "*"]]
	for replacement in replacements
		expression = replace(replacement[0], replacement[1], expression)
	return (eval(expression) ?? 0) cover Void
}

reevaluate_turn = () ->
{
	players = len(names)
	next = 0
	for i from turn till 2 * players
		if scores[i % players] != 0 {
			next = i % players
			break
		}
	global turn = next
}

# Commands.

commands = [
	[
		"type", "<amount>", "Set game type",
		amount -> {
			new = sum(amount)
			if new != Void
				global type = new
		}
	],
	[
		"add", "<name>", "Add player",
		name -> {
			global names += [name]
			global scores += [type]
			global plays += [""]
		}
	],
	[
		"kick", "<name>", "Remove player",
		name -> {
			index = find(names, name)
			global names = pop(names, index)
			global scores = pop(scores, index)
			global plays = pop(plays, index)
		}
	],
		[
		"fix", "<score> [<name>]", "Fix score",
		score_and_name -> {
			separation = Void
			for i from 0 till len(score_and_name)
				if score_and_name[i] == " "
					separation = i
			if separation == Void {
				name = names[turn]
				score = score_and_name
			} else {
				name = score_and_name[separation+1..]
				score = score_and_name[..separation]
			}
			new = sum(score)
			if new != Void {
				index = find(names, name)
				if index != Void
					global scores[index] = new
			}
		}
	],
	[
		"reset", "", "Reset all scores",
		_ -> {
			for i from 0 till len(scores) {
				global scores[i] = type
				global plays[i] = ""
			}
		}
	],
	[
		"?", "", "Toggle this board",
		_ -> {
			global show_help = !show_help
		}
	]
]

# Setup.

colors = [
	[00, ["1f", "1c", "1b"]], # Background.
	[10, ["00", "e6", "41"]], # Player: Active.
	[02, ["8a", "7f", "7c"]], # Player: Idle.
	[11, ["f0", "c6", "1d"]], # Player: Winner.
	[15, ["ff", "ff", "ff"]], # Score.
	[13, ["00", "e6", "41"]], # Prompt.
	[05, ["8a", "7f", "7c"]], # Prompt, recent.
	[08, ["5f", "57", "54"]], # Type.
	[04, ["12", "10", "10"]], # Commands board background.
	[12, ["5e", "55", "52"]], # Commands board text.
]
for color in colors
	put("\e]4;{color[0]};rgb:{join(color[1], "/")}\a")

command_names = []
command_parameters = []
command_descriptions = []
for command in commands {
	command_names += [command[0]]
	command_parameters += [command[1]]
	command_descriptions += [command[2]]
}
widest_command_name = widest(command_names)
widest_command_parameters = widest(command_parameters)
widest_command_description = widest(command_descriptions)

type = 301
names = []
scores = []
plays = []
turn = 0
show_help = True

while True {
	buffer = "\e[H\e[?12l\e[40m\e[K\n"
	buffer += "\e[K  \e[90mDARTS {type}\n\e[K\n"
	stop_1 = 3 + widest(names) + 3
	stop_2 = stop_1 + widest(scores) + 3
	if !len(names)
		buffer += "\e[K  \e7\n"
	else
		for i from 0 till len(names) {
			buffer += "\e[K  "
			if scores[i] == 0
				buffer += "\e[93m\e[9m"
			elif i == turn
				buffer += "\e[92m\e[1m"
			else
				buffer += "\e[32m"
			buffer += names[i] + "\e[{stop_1}G\e[29m\e[22m\e[97m{i == turn ? "\e[5m" : ""}{scores[i]}\e[25m"
			if i == turn
				buffer += "\e[{stop_2}G\e7"
			elif plays[i]
				buffer += "\e[{stop_2}G  \e[35m{plays[i]} / {sum(plays[i])}"
			buffer += "\n"
		}
	if show_help {
		stop_1 = 6 + widest_command_name + 3
		stop_2 = stop_1 + widest_command_parameters + 3
		stop_3 = stop_2 + widest_command_description + 3
		buffer += "\e[K\n\e[K  \e[44m\e[94m\e[K\e[{stop_3}G\e[40m\e[K\n"
		for command in commands
			buffer += "\e[K  \e[44m\e[K   {command[0]}\e[{stop_1}G{command[1]}\e[{stop_2}G\e[3m{command[2]}\e[23m\e[{stop_3}G\e[40m\e[K\n"
		buffer += "\e[K  \e[44m\e[K\e[{stop_3}G\e[40m\e[K\n"
	}
	buffer += "\e[J\e[H"
	print(buffer)
	
	# Get request.
	request = input("\e8\e[95m\e[?25h> ")
	put("\e[?25l")
	
	# Process request.
	command_function = Void
	argument = Void
	for command in commands
		if request[..len(command[0])] == command[0] {
			command_function = command[3]
			argument = request[len(command[0])+1..]
			break
		}
	
	# Execute request or log points and advance turn.
	if command_function {
		command_function(argument)
	} elif len(names) > 0 {
		points = sum(request)
		if points == Void
			continue
		score = scores[turn] - points
		if score >= 0 {
			scores[turn] = score
			plays[turn] = request
		}
		global turn += 1
	}
	reevaluate_turn()
}
